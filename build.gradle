buildscript {
    repositories {
        jcenter()
        mavenCentral()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

allprojects {
    version = VERSION_NAME
    group = GROUP

    repositories {
        mavenLocal()
        jcenter()
    }
}

ext.libraries = [
        app_compat: 'com.android.support:appcompat-v7:22.1.1',
        testing: [
            dependencies.create('junit:junit:4.11') {
                exclude module: 'hamcrest-core'
            },
            'org.robolectric:robolectric:3.0-rc3',
            'com.google.guava:guava:18.0',
            'org.hamcrest:hamcrest-integration:1.3',
            'org.hamcrest:hamcrest-core:1.3',
            'org.hamcrest:hamcrest-library:1.3',
            'org.mockito:mockito-core:1.10.19',
            dependencies.create('com.squareup:fest-android:1.0.+') {
                exclude group: 'com.android.support', module: 'support-v4'
            }
        ]
]

ext.compile = [
        minSdk: 14,
        maxSdk: 22,
        buildToolsVersion: "21.1.2"
]

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion compile.maxSdk
    buildToolsVersion compile.buildToolsVersion

    defaultConfig {
        minSdkVersion compile.minSdk
        targetSdkVersion compile.maxSdk
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.auth0.android:identity-core:1.7.0'
    compile libraries.app_compat
    compile 'com.google.android.gms:play-services-plus:6.5.87'
}

apply from: './maven_push.gradle'